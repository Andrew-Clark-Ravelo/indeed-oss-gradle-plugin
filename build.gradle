if (gradle.getParent() != null) {
    plugins.apply 'java-gradle-plugin'
} else {
    plugins.apply 'com.indeed.oss'
    indeedOss.activateFeature 'gradle-plugin'
    pluginBundle {
        website = 'https://github.com/indeedeng/indeed-oss-gradle-plugin'
        vcsUrl = 'https://github.com/indeedeng/indeed-oss-gradle-plugin'
        tags = ['indeed', 'opensource']
    }
    indeedLibrary.name = 'indeed-oss-gradle-plugin'
}

gradlePlugin {
    plugins {
        ossPlugin {
            id = 'com.indeed.oss'
            displayName = 'Indeed OSS Plugin'
            description = 'Helper plugin for all open-source projects produced by Indeed'
            implementationClass = 'com.indeed.ossgradle.IndeedOssGradlePlugin'
        }
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r'

    implementation 'com.gradle.plugin-publish:com.gradle.plugin-publish.gradle.plugin:0.18.0'
    implementation 'com.diffplug.gradle.spotless:com.diffplug.gradle.spotless.gradle.plugin:6.0.5'
}
